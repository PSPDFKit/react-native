/*
 * build.gradle
 *
 *   Nutrient
 *
 *   Copyright Â© 2021-2026 PSPDFKit GmbH. All rights reserved.
 *
 *   THIS SOURCE CODE AND ANY ACCOMPANYING DOCUMENTATION ARE PROTECTED BY INTERNATIONAL COPYRIGHT LAW
 *   AND MAY NOT BE RESOLD OR REDISTRIBUTED. USAGE IS BOUND TO THE PSPDFKIT LICENSE AGREEMENT.
 *   UNAUTHORIZED REPRODUCTION OR DISTRIBUTION IS SUBJECT TO CIVIL AND CRIMINAL PENALTIES.
 *   This notice may not be removed from this file.
 */

/*
 *   Contains gradle configuration constants
 */
ext {
    NUTRIENT_VERSION = '11.0.0'
}

buildscript {
    ext.kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.kotlinVersion :
            rootProject.ext.has('kotlin_version') ? rootProject.ext.kotlin_version :
                    '1.9.24'

    ext.getComposeVersion = { kotlin_version ->
        switch (kotlin_version) {
            case ~/2\..*/:
                // For Kotlin 2.x+, compose compiler is handled by the plugin
                return null
            case '1.9.25': return '1.5.15'
            case '1.9.24': return '1.5.14'
            default: return '1.5.14'
        }
    }

    repositories {
        mavenCentral()
        google()
        maven {
            url 'https://maven.google.com'
        }
        maven {
            url 'https://my.nutrient.io/maven/'
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.5.0")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // Add Compose compiler plugin for Kotlin 2.0+
        if (kotlin_version.startsWith('2')) {
            classpath "org.jetbrains.kotlin:compose-compiler-gradle-plugin:$kotlin_version"
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
// Add Compose compiler plugin for Kotlin 2.0+
if (kotlin_version.startsWith('2')) {
    apply plugin: 'org.jetbrains.kotlin.plugin.compose'
}

def isNewArchitectureEnabled() {
    // To opt-in for the New Architecture, you can either:
    // - Set `newArchEnabled` to true inside the `gradle.properties` file
    // - Invoke gradle with `-newArchEnabled=true`
    // - Set an environment variable `ORG_GRADLE_PROJECT_newArchEnabled=true`
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}
android {
    compileSdkVersion 34

    namespace "com.pspdfkit.react"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            java.srcDirs = ["src/main/java"]
            // Conditionally include newarch sources only when New Architecture is enabled
            if (isNewArchitectureEnabled()) {
                java.srcDirs += ["src/newarch/java"]
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    composeOptions {
        def composeVersion = getComposeVersion(kotlin_version)
        if (composeVersion != null) {
            kotlinCompilerExtensionVersion composeVersion
        }
        // When using Kotlin 2.0+, the kotlinCompilerExtensionVersion is handled by the plugin
    }

    kotlin {
        jvmToolchain(17)
    }
}

configurations.all {
    resolutionStrategy {
        force "androidx.compose.foundation:foundation:1.9.4"
        force "androidx.compose.ui:ui:1.9.4"
        force "androidx.compose.runtime:runtime:1.9.4"
        force "androidx.compose.material:material:1.9.4"
        force "androidx.compose.material3:material3:1.4.0"
    }
}

dependencies {
    api("io.nutrient:nutrient:${NUTRIENT_VERSION}") {
        exclude group: 'com.google.auto.value', module: 'auto-value'
        exclude group: 'androidx.recyclerview', module: 'recyclerview'
    }

    implementation "com.facebook.react:react-native:+"

    // OkHttp dependencies
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    implementation "androidx.recyclerview:recyclerview:1.4.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx3:1.7.3"

    // Explicitly declare Compose Foundation to ensure version 1.9.4 (matches PSPDFKit SDK)
    implementation "androidx.compose.foundation:foundation:1.9.4"

    // Let PSPDFKit SDK manage Compose versions - only specify lifecycle-viewmodel-compose to match SDK requirement
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.9.4"

    // AI Assistant dependencies
    implementation("io.noties.markwon:core:4.6.2")
    implementation("io.noties.markwon:html:4.6.2")
    implementation("io.noties.markwon:linkify:4.6.2")
    implementation("io.noties.markwon:ext-tables:4.6.2")
    implementation("io.noties.markwon:ext-strikethrough:4.6.2")
    implementation("io.socket:socket.io-client:2.1.2")
}
