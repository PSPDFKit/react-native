/*
 * build.gradle
 *
 *   PSPDFKit
 *
 *   Copyright Â© 2021-2025 PSPDFKit GmbH. All rights reserved.
 *
 *   THIS SOURCE CODE AND ANY ACCOMPANYING DOCUMENTATION ARE PROTECTED BY INTERNATIONAL COPYRIGHT LAW
 *   AND MAY NOT BE RESOLD OR REDISTRIBUTED. USAGE IS BOUND TO THE PSPDFKIT LICENSE AGREEMENT.
 *   UNAUTHORIZED REPRODUCTION OR DISTRIBUTION IS SUBJECT TO CIVIL AND CRIMINAL PENALTIES.
 *   This notice may not be removed from this file.
 */

/*
 *   Contains gradle configuration constants
 */
ext {
    PSPDFKIT_VERSION = '10.0.1'
}

buildscript {
    ext.kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.kotlinVersion :
            rootProject.ext.has('kotlin_version') ? rootProject.ext.kotlin_version :
                    '1.9.24'

    ext.getComposeVersion = { kotlin_version ->
        switch (kotlin_version) {
            case ~/2\..*/: 
                // For Kotlin 2.x+, compose compiler is handled by the plugin
                return null
            case '1.9.25': return '1.5.15'
            case '1.9.24': return '1.5.14'
            default: return '1.5.14'
        }
    }

    repositories {
        mavenCentral()
        google()
        maven {
            url 'https://maven.google.com'
        }
        maven {
            url 'https://my.nutrient.io/maven/'
        }
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.5.0")
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // Add Compose compiler plugin for Kotlin 2.0+
        if (kotlin_version.startsWith('2')) {
            classpath "org.jetbrains.kotlin:compose-compiler-gradle-plugin:$kotlin_version"
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
// Add Compose compiler plugin for Kotlin 2.0+
if (kotlin_version.startsWith('2')) {
    apply plugin: 'org.jetbrains.kotlin.plugin.compose'
}

android {
    compileSdkVersion 34

    namespace "com.pspdfkit.react"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lintOptions {
        abortOnError false
    }

    buildFeatures {
        buildConfig true
        compose true
    }

    composeOptions {
        def composeVersion = getComposeVersion(kotlin_version)
        if (composeVersion != null) {
            kotlinCompilerExtensionVersion composeVersion
        }
        // When using Kotlin 2.0+, the kotlinCompilerExtensionVersion is handled by the plugin
    }

    kotlin {
        jvmToolchain(17)
    }
}

dependencies {
    api("io.nutrient:nutrient:${PSPDFKIT_VERSION}") {
        exclude group: 'com.google.auto.value', module: 'auto-value'
        exclude group: 'androidx.recyclerview', module: 'recyclerview'
    }

    implementation "com.facebook.react:react-native:+"
    implementation 'com.squareup.okhttp3:okhttp:4.9.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.compose.material:material:1.7.8"
    implementation "androidx.compose.material3:material3:1.3.1"
    implementation "androidx.recyclerview:recyclerview:1.3.2"
}
